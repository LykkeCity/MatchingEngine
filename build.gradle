group 'com.lykke.me'
version '0.1'

buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

apply plugin: "idea"
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.google.protobuf"

compileKotlin.dependsOn ':generateProto'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    // internal libs
    compile "org.bitbucket.lykke:kotlin-utils:0.0.5-SNAPSHOT"

    // external libs
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'log4j:log4j:1.2.17'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-2'
    compile 'com.microsoft.azure:azure-storage:4.0.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'com.rabbitmq:amqp-client:3.6.5'
    compile 'de.ruedigermoeller:fst:2.52'
    compile 'redis.clients:jedis:2.9.0'

    testCompile  'junit:junit:4.11'
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

compileKotlin {
    kotlinOptions {
        apiVersion = "1.2"
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }

    generateProtoTasks.generatedFilesBaseDir = 'src'

    generateProtoTasks {
        ofSourceSet('main')
    }
}

task printProperties << {
    teamcity.each { key, val ->
        println "##tc-property name='${key}' value='${val}'"
    }
}

jar {
    if (project.hasProperty('teamcity')) {
        manifest {
            attributes 'Revision-number': teamcity['build.vcs.number'], 'Build-number': teamcity['build.number'], 'Version': version + '.' + teamcity['build.number']
        }
    } else {
        manifest {
            attributes 'Revision-number': 'localBuild', 'Build-number': 'localBuild', 'Version': version + '.000'
        }
    }
}

mainClassName = "com.lykke.matching.engine.AppStarterKt"
applicationDefaultJvmArgs = ["-Xms512m", "-Xmx1g"]
distTar.enabled = false